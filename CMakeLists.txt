cmake_minimum_required(VERSION 3.16)

project(AwesomeNbtViewer
    VERSION 0.1.0
    LANGUAGES C CXX
)

### Additional flags
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

### Version
set(AWESOMENBTVIEWER_VERSION_MAJOR ${PROJECT_VERSION_MAJOR})
set(AWESOMENBTVIEWER_VERSION_MINOR ${PROJECT_VERSION_MINOR})
set(AWESOMENBTVIEWER_VERSION_PATCH ${PROJECT_VERSION_PATCH})
set(AWESOMENBTVIEWER_VERSION ${AWESOMENBTVIEWER_VERSION_MAJOR}.${AWESOMENBTVIEWER_VERSION_MINOR}.${AWESOMENBTVIEWER_VERSION_PATCH})

execute_process(
  COMMAND git rev-parse --abbrev-ref HEAD
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
  OUTPUT_VARIABLE AWESOMENBTVIEWER_GIT_BRANCH
  OUTPUT_STRIP_TRAILING_WHITESPACE
  ERROR_QUIET
)
execute_process(
  COMMAND git log -1 --format=%h
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
  OUTPUT_VARIABLE AWESOMENBTVIEWER_GIT_HASH
  OUTPUT_STRIP_TRAILING_WHITESPACE
  ERROR_QUIET
)

configure_file(
    ${CMAKE_CURRENT_SOURCE_DIR}/src/version.hpp.in
    ${PROJECT_BINARY_DIR}/version.hpp
)

### Qt Stuff
set(CMAKE_AUTOUIC ON)
set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTORCC ON)

### Installation directories
set(AwesomeNbtViewer_INSTALL_BINARY_DIR "bin")
set(AwesomeNbtViewer_INSTALL_INCLUDE_DIR "include")
set(AwesomeNbtViewer_INSTALL_LIBRARY_DIR "lib/${CMAKE_BUILD_TYPE}")
set(AwesomeNbtViewer_INSTALL_CMAKE_CONFIG_DIR "share/AwesomeNbtViewer/cmake")
set(AwesomeNbtViewer_INSTALL_ADDITIONAL_FILES_DIR "share/AwesomeNbtViewer")

### Search and include necessary packages
find_package(Qt6 REQUIRED COMPONENTS Widgets Core)
find_package(AwesomeMC 0.3.3 REQUIRED)

### AwesomeNbtViewer
add_subdirectory(src)

### Install
install(FILES "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE" "${CMAKE_CURRENT_SOURCE_DIR}/readme.md" DESTINATION ${AwesomeNbtViewer_INSTALL_ADDITIONAL_FILES_DIR})